import org.apache.tools.ant.filters.ReplaceTokens

//use -Phostname="" to change advertised host for configuring influxdb and grafana
if (!project.hasProperty("hostname")) {
    ext.hostname = "localhost"
}

task install(type: Exec, dependsOn: ["copyScripts", "copyConfigs"]) {
    executable "$buildDir/scripts/install-kafka-metrics-instance.sh"
}

task copyScripts(type: Copy, overwrite: true) {
    from new File("$projectDir/src")
    into new File("$buildDir/scripts")
    filter(ReplaceTokens, tokens: [BASE_DIR: project.projectDir.toString()])
    include('*.sh')
    fileMode 0777
}

task copyConfigs(dependsOn: ["copyInfluxDbConfig", "copyGrafanaConfig"])

task copyInfluxDbConfig(type: Copy) {
    from new File("$projectDir/src")
    into new File("$buildDir/conf")
    filter(ReplaceTokens, tokens: [
            DATA_DIR: project.projectDir.toString() + '/.data/influxdb',
            HOSTNAME: project.ext.hostname
    ])
    include('influxdb.conf')
}

task copyGrafanaConfig(type: Copy) {
    from new File("$projectDir/src")
    into new File("$buildDir/conf")
    filter(ReplaceTokens, tokens: [
            DATA_DIR: project.projectDir.toString() + '/.data/grafana',
            HOSTNAME: project.ext.hostname
    ])
    include('grafana.ini')
}
